
import "game_object.znn";
import "std/math.znn";
import "std/time.znn";
import "game.znn";
export import "object_filter.znn";
export import "std/box.znn";
export import "std/vector.znn";
export import "ui/vec2.znn";
export import "glyph.znn";

export struct HealEffect {
  double amount;
};

HealEffect copy(HealEffect*) = default;

void apply(HealEffect* effect, GameObject mutable* object, Game mutable* game) {
  if (!!object^.health && *object^.health < *object^.attributes.maxHealth) {
    *object^.health = min(*object^.attributes.maxHealth, *object^.health + effect^.amount);
    const curTime = current_time_milli();
    game^.getLevel()^.animations.push_back(LevelAnimation(LevelAnimationType::heal(HealAnimation()),
        object^.pos, curTime));
  }
}

export struct WoundEffect {
  double amount;
};

WoundEffect copy(WoundEffect*) = default;

void apply(WoundEffect* effect, GameObject mutable* object, Game mutable* game) {
  const curTime = current_time_milli();
  const bloodAnim = [&] { return LevelAnimationType::blood(BloodAnimation(object^.attributes.bloodColor)); };
  if (object^.health) {
    game^.getLevel()^.animations.push_back(LevelAnimation(bloodAnim(), object^.pos, curTime));
    *object^.health -= effect^.amount;
  }
  if (object^.health ?? 1.0 <= 0.0) {
    for (i : range(0, 10))
      game^.getLevel()^.animations.push_back(LevelAnimation(bloodAnim(), object^.pos, curTime + i));
    object^.isDead = true;
  }
}

export struct FilterEffect {
  ObjectFilter filter;
  box<ObjectEffect> effect;
};

FilterEffect copy(FilterEffect*) = default;

void apply(FilterEffect* effect, GameObject mutable* object, Game mutable* game) {
  if (effect^.filter.apply(object))
    effect^.effect^.apply(object, game);
}

export struct RemoveEffect {
};

RemoveEffect copy(RemoveEffect*) = default;

void apply(RemoveEffect* effect, GameObject mutable* object, Game mutable* game) {
  object^.isDead = true;
}

export struct RadiusEffect {
  double radius;
  box<ObjectEffect> effect;
};

RadiusEffect copy(RadiusEffect*) = default;

void apply(RadiusEffect* effect, vec2d pos, Game mutable* game) {
  for (obj : *game^.getObjects())
    if ((obj^^.pos - pos).length_d() <= effect^.radius)
      effect^.effect^.apply(obj^.get(), game);
}

export struct AllEffect {
  vector<ObjectEffect> elems;
};

AllEffect copy(AllEffect*) = default;

void apply(AllEffect* effect, vec2d pos, Game mutable* game) {
  for (it : effect^.elems)
    it^.apply(pos, game);
}

export struct ProjectileEffect {
  double dir;
  double speed;
  double size;
  double lifeTime;
  box<ObjectEffect> effect;
  Glyph glyph;
};

ProjectileEffect copy(ProjectileEffect*) = default;

void apply(ProjectileEffect* effect, vec2d pos, Game mutable* game) {
  game^.spawnProjectile(null, pos, from_polar(effect^.dir, effect^.speed), effect^.glyph.copy(), effect^.size,
      effect^.effect^.copy(), effect^.lifeTime);
}

export struct AddEffect {
  string id;
};

AddEffect copy(AddEffect*) = default;

void apply(AddEffect* effect, vec2d pos, Game mutable* game) {
  game^.addObject(pos, effect^.id);
}

export struct OffsetEffect {
  vec2d offset;
  box<ObjectEffect> effect;
};

OffsetEffect copy(OffsetEffect*) = default;

void apply(OffsetEffect* effect, vec2d pos, Game mutable* game) {
  effect^.effect^.apply(pos + effect^.offset, game);
}

concept<T> CreatureEffect {
  void apply(T*, GameObject mutable*, Game mutable*);
};

concept<T> PositionEffect {
  void apply(T*, vec2d, Game mutable*);
};

bool objectContains(GameObject* obj, vec2d pos) {
  return !!obj^.attributes.box && obj^.attributes.box^.isPenetrated(obj^.pos - pos);
}

template <T> requires CreatureEffect<T>
void apply(T* t, vec2d pos, Game mutable* game) {
  for (it : *game^.getObjects()) {
    const obj = it^.get();
    if (objectContains(obj, pos))
      t^.apply(obj, game);
  }
}

vec2d getPos(GameObject* obj) {
  return obj^.pos;
}

template <T> requires CreatureEffect<T>
void apply(T* t, GameObject mutable* object, Game mutable* game) {
  t^.apply(getPos(object), game);
}

export union ObjectEffect {
  HealEffect heal;
  WoundEffect wound;
  FilterEffect filter;
  RemoveEffect remove;
  RadiusEffect radius;
  ProjectileEffect projectile;
  AddEffect add;
  AllEffect all;
  OffsetEffect offset;
};

export ObjectEffect copy(ObjectEffect*) = default;

export void apply(virtual ObjectEffect*, vec2d, Game mutable*) {}
export void apply(virtual ObjectEffect*, GameObject mutable*, Game mutable*) {}
