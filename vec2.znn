
export import "direction.znn";
import "std/io.znn";
import "std/enum.znn";
import "std/math.znn";

export struct vec2 {
  int x;
  int y;
};

export struct vec2d {
  double x;
  double y;
};

export vec2 operator + (vec2* a, vec2* b) {
  return vec2(a->x + b->x, a->y + b->y);
}

export vec2 operator / (vec2* a, int* b) {
  return vec2(a->x / *b, a->y / *b);
}

export vec2 operator * (vec2* a, int* b) {
  return vec2(a->x * *b, a->y * *b);
}

export vec2 operator - (vec2* a, vec2* b) {
  return vec2(a->x - b->x, a->y - b->y);
}

export vec2 operator - (vec2* a) {
  return vec2(-a->x, -a->y);
}

export bool operator == (vec2* a, vec2* b) {
  return a->x == b->x && a->y == b->y;
}

export vec2 implicit_copy(vec2*) = default;

export string to_string(vec2* v) {
  return "(" + to_string(&v->x) + ", " + to_string(&v->y) + ")";
}

export double length_d(vec2* v) {
  return sqrt(v->x * v->x + v->y * v->y);
}

export double angle(vec2* v) {
  return atan2(v->x, v->y);
}

export vec2[4] neighbors4(vec2* v) {
  return { vec2(v->x + 1, v->y), vec2(v->x, v->y + 1), vec2(v->x - 1, v->y), vec2(v->x, v->y - 1) };
}

export vec2d operator + (vec2d* a, vec2d* b) {
  return vec2d(a->x + b->x, a->y + b->y);
}

export vec2d operator / (vec2d* a, double* b) {
  return vec2d(a->x / *b, a->y / *b);
}

export vec2d operator * (vec2d* a, double* b) {
  return vec2d(a->x * *b, a->y * *b);
}

export vec2 as_int(vec2d* v) {
  return vec2(v->x.as_int(), v->y.as_int());
}

export vec2d as_double(vec2* v) {
  return vec2d(v->x.as_double(), v->y.as_double());
}

export vec2d operator - (vec2d* a, vec2d* b) {
  return vec2d(a->x - b->x, a->y - b->y);
}

export vec2d operator - (vec2d* a) {
  return vec2d(-a->x, -a->y);
}

export bool operator == (vec2d* a, vec2d* b) {
  return a->x == b->x && a->y == b->y;
}

export vec2d implicit_copy(vec2d*) = default;

export string to_string(vec2d* v) {
  return "(" + to_string(&v->x) + ", " + to_string(&v->y) + ")";
}

export double length_d(vec2d* v) {
  return sqrt(v->x * v->x + v->y * v->y);
}

export double angle(vec2d* v) {
  return atan2(v->x, v->y);
}

